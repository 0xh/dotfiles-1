#!/usr/bin/env zsh
################################################################################
#    Author: Wenxuan Zhang                                                     #
#     Email: wenxuangm@gmail.com                                               #
#   Created: 2020-01-03 16:33                                                  #
################################################################################
# zmodload zsh/zprof
bindkey -e # This line should be on top

# simple color logger
loginfo()  { printf "%b[info]%b %s\n"  '\e[0;32m\033[1m' '\e[0m' "$@" >&2; }
logwarn()  { printf "%b[warn]%b %s\n"  '\e[0;33m\033[1m' '\e[0m' "$@" >&2; }
logerror() { printf "%b[error]%b %s\n" '\e[0;31m\033[1m' '\e[0m' "$@" >&2; }
synchronized() {
    local lock="$1" handler="$2" fallback="$3"
    [ -e "$lock" ] && kill -0 $(cat "$lock") && return 1

    trap "rm -f $lock" INT TERM EXIT
    echo $$ > "$lock"

    eval "$handler"

    rm -f "$lock"
}

#######################################################################
# plugin manager                                                      #
#######################################################################
[ -d ~/.zplugin/bin ] || {
    mkdir -p ~/.zplugin
    git clone --depth=1 https://github.com/zdharma/zplugin.git ~/.zplugin/bin || exit 1
}
source "$HOME/.zplugin/bin/zplugin.zsh"

zpl ice depth='1'; zpl light 'romkatv/zsh-defer'

#######################################################################
# theme                                                               #
#######################################################################
source <(starship init zsh) || logwarn "failed loading starship."
# 'denysdovhan/spaceship-prompt'
# 'bhilburn/powerlevel9k'
# 'subnixr/minimal'
# 'mafredri/zsh-async'
# 'sindresorhus/pure'

cached_source() {
    local rc_gen_cmd="$1"
    local dir="/tmp/zsh_cache/$(id -u)"
    local file="$dir/$rc_gen_cmd.zsh"
    local lock="$dir/lock"
    local fallback=
    local handler="
        [ -f '$file' ] && return
        mkdir -p '$dir'
        eval '$rc_gen_cmd' > '$file'
        zcompile '$file'
    "
    mkdir -p "$dir"
    if [ ! -f "$file" ]; then
        if ! synchronized "$lock" "$handler"; then
            source <($rc_gen_cmd) && return
        fi
    fi
    source "$file"
}

__zsh_async_source_task__() {
    [ -f ~/.fzf.zsh       ] && source ~/.fzf.zsh
    [ -f ~/.zsh_fzf_extra ] && source ~/.zsh_fzf_extra
    #######################################################################
    # normal plugins                                                      #
    #######################################################################
    # forgit should be loaded after .zsh_fzf_extra to read fzf's env
    FORGIT_FZF_DEFAULT_OPTS="--height '80%'"
    ZSH_AUTOSUGGEST_PARTIAL_ACCEPT_WIDGETS=(forward-word forward-char)
    ZSH_AUTOSUGGEST_ACCEPT_WIDGETS=(end-of-line)
    zpl light-mode lucid depth='1' for         \
        atload='_zsh_autosuggest_start'        \
            'zsh-users/zsh-autosuggestions'    \
        blockf                                 \
            'zsh-users/zsh-completions'        \
            'zdharma/fast-syntax-highlighting' \
            'hlissner/zsh-autopair'            \
            'rupa/z'                           \
            'wfxr/forgit'                      \
            'wfxr/forepos'                     \
            'wfxr/epoch-cli'                   \
            'wfxr/formarks'                    \
            'soimort/translate-shell'          \

    # 'skywind3000/z.lua' # better than rupa/z but require lua?

    #######################################################################
    # snippet plugins                                                     #
    #######################################################################
    zpl ice as='null' cloneonly depth='1' nocompile; zpl light 'ohmyzsh/ohmyzsh'
    OMZ="${ZPLGM[PLUGINS_DIR]}/ohmyzsh---ohmyzsh/plugins"

    zpl snippet "$OMZ/git-extras/git-extras.plugin.zsh"
    zpl snippet "$OMZ/git-flow-avh/git-flow-avh.plugin.zsh"
    zpl snippet "$OMZ/mvn/mvn.plugin.zsh"
    zpl snippet "$OMZ/colored-man-pages/colored-man-pages.plugin.zsh"
    zpl ice as='completion'; zpl snippet "$OMZ/docker/_docker"
    zpl ice as='completion'; zpl snippet "$OMZ/gem/_gem"
    zpl ice as='completion'; zpl snippet "$OMZ/lein/_lein"

    #######################################################################
    # programs                                                            #
    #######################################################################
    # zlk <from> [dest]
    zlk() {
        local src="$1"; local dest="$src"
        [ $# -eq 2 ] && dest="$2"
        eval 'ln -sf $PWD/'"$src ~/bin/$dest"
    }

    zpl light-mode as='null' cloneonly depth='1' nocompile for                                             \
        atclone='zlk src/ydcv.py ydcv'                                                                     \
            'felixonmars/ydcv'                                                                             \
        atclone='zlk iconful.sh iconful'                                                                   \
            'wfxr/iconful'                                                                                 \
        atclone='zlk b'                                                                                    \
            'wfxr/fzf-chrome-marks'                                                                        \
        atclone='zlk diff-so-fancy && git config --global core.pager "diff-so-fancy | less --tabs=4 -RFX"' \
            'so-fancy/diff-so-fancy'                                                                       \
        atclone="zlk httpstat.py httpstat"                                                                 \
            'reorx/httpstat'                                                                               \

    EMOJI_CLI_KEYBIND='^[m'
    zpl light-mode as='null' cloneonly depth='1' for                                                       \
        atclone='zlk emojify; zlk fuzzy-emoji' multisrc='*.zsh'                                            \
            'wfxr/emoji-cli'                                                                               \

    zpl light-mode as='null' from='gh-r' nocompile for                                                     \
        if='[[ -z $+commands[jq] ]]' mv="jq* -> ~/bin/jq"                                                  \
            'stedolan/jq'                                                                                  \

    source ~/.zsh_misc
    source ~/.zsh_aliases
    [ -f ~/.travis/travis.sh ] && source ~/.travis/travis.sh
    [ -f ~/.zsh_local        ] && source ~/.zsh_local
    [ -f ~/.zsh_secret       ] && source ~/.zsh_secret 2>/dev/null # maybe locked

    # https://gist.github.com/ctechols/ca1035271ad134841284
    custom_compinit() {
        autoload -Uz compinit
        # once a day
        for dump in ~/.zcompdump(N.mh+24); do
            compinit; zpcdreplay; return
        done
        compinit -C; zpcdreplay
    }; custom_compinit

    (( $+commands[kubectl] )) && cached_source 'kubectl completion zsh'
    (( $+commands[nodenv]  )) && cached_source 'nodenv init - zsh'
    (( $+commands[pyenv]   )) && cached_source 'pyenv init - zsh'
    (( $+commands[rbenv]   )) && cached_source 'rbenv init - zsh'
    (( $+commands[pip]     )) && cached_source 'pip completion --zsh' # fucking slow. cache it!

    # some custom functions need comdef which was loaded after compinit
    source ~/.zsh_custom

    unset -f zlk custom_compinit
}

# disable defer loading to view logs, useful when initializing zsh first time
if [[ -n $DISABLE_ZSH_DEFER ]]; then
    __zsh_async_source_task__
else
    zsh-defer -c __zsh_async_source_task__
fi

# zprof
return 0
